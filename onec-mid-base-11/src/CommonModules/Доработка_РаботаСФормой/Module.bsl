#Область СлужебныйПрограммныйИнтерфейс

Процедура  ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавлениеКомандыПересчитатьСУчетомСкидки(Форма);
		ДобавлениеЭлементаСогласованнаяСкидка(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьКонтактноеЛицоПередЭлементом(Форма, Форма.Элементы.Основание);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьКонтактноеЛицоПередЭлементом(Форма, Форма.Элементы.СуммаДокумента);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Доработка_КонтактноеЛицо"; 
	
КонецПроцедуры 

Процедура ВставитьКонтактноеЛицоПередЭлементом(Форма, ЭлементПосле)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , ЭлементПосле);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Доработка_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ДобавлениеКомандыПересчитатьСУчетомСкидки(Форма)
	
	Команда = Форма.Команды.Добавить("ПересчитатьСУчетомСкидки");
	Команда.Заголовок = ("Пересчитать с учетом скидки");
	Команда.Действие = ("КомандаПересчитатьСУчетомСкидки");
	
	КнопкаКоманды = Форма.Элементы.Добавить("КомандаПересчитатьСУчетомСкидки", Тип("КнопкаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	КнопкаКоманды.ИмяКоманды = "ПересчитатьСУчетомСкидки";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
Конецпроцедуры 

Процедура ДобавлениеЭлементаСогласованнаяСкидка(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Доработка_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ПриИзмененииСогласованнойСкидки");
	
КонецПроцедуры


#КонецОбласти




